apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    compileOptions {
        encoding "UTF-8"
    }
    defaultConfig {
        applicationId "me.devsaki.hentoid"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 40
        versionName '1.2.0r3_dev'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'README.md'
        exclude 'LICENSE'
        exclude 'update.json'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            shrinkResources true

            // Library specific proguard files
            FileCollection proGuardFileCollection = files { file('../.proguard/').listFiles() }
            proguardFiles(proGuardFileCollection)

            // Default proguard files & project app specific rules
            //noinspection GroovyAssignabilityCheck
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project-app.pro'

            // As of Gradle Android plugin 1.1.0, the test APK has a separate config
            testProguardFile 'proguard-project-test.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true

            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release

            // Library specific proguard files
            FileCollection proGuardFileCollection = files { file('../.proguard/').listFiles() }
            proguardFiles(proGuardFileCollection)

            // Default proguard files & project app specific rules
            //noinspection GroovyAssignabilityCheck
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project-app.pro'

            // As of Gradle Android plugin 1.1.0, the test APK has a separate config
            testProguardFile 'proguard-project-test.pro'
        }
    }
    lintOptions {
        lintConfig file("../lint.xml")
    }
}

def Properties props = new Properties()
def propFile = new File('.signing/signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD')
            && props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    // Compile any included libraries
    compile fileTree(include: ['*jar'], dir: 'libs')
    // Tests
    testCompile 'junit:junit:4.12'
    // Support libraries
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    // Material Dialogs
    compile 'com.afollestad.material-dialogs:core:0.8.5.9'
    // Image loader
    compile 'com.github.bumptech.glide:glide:3.8.0-SNAPSHOT'
    // Integrate with OkHttp
    compile('com.github.bumptech.glide:okhttp-integration:1.5.0-SNAPSHOT') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
    }
    // Intro screens or slides
    compile 'com.github.paolorotolo:appintro:4.0.0'
    // Google Play Services Analytics
    compile 'com.google.android.gms:play-services-analytics:9.2.1'
    // Java serialization/deserialization (Java Objects into JSON and back)
    compile 'com.google.code.gson:gson:2.6.2'
    // Download app updates (without user prompts)
    compile 'com.mani:ThinDownloadManager:1.2.5'
    // Additional Support Libraries
    // HTTP & HTTP/2 client
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    // IO functionality
    compile 'commons-io:commons-io:2.5'
    // EventBus
    compile 'org.greenrobot:eventbus:3.0.0'
    // JSOUP HTML parser
    compile 'org.jsoup:jsoup:1.9.2'
    // RxAndroid (RxJava bindings for Android)
    compile 'io.reactivex:rxandroid:1.2.0'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.5'
}

apply plugin: 'com.google.gms.google-services'
