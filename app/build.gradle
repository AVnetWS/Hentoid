apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    compileOptions {
        encoding "UTF-8"
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    defaultConfig {
        applicationId "me.devsaki.hentoid"
        minSdkVersion 15
        // Hold updating until implementing an image viewer
        // or Perfect Viewer implements Content URI
        //noinspection OldTargetApi
        targetSdkVersion 23
        versionCode 43
        versionName '1.2.1r3'
        resConfigs "en"
        vectorDrawables.useSupportLibrary = true
    }
    packagingOptions {
        exclude 'README.md'
        exclude 'LICENSE'
        exclude 'update.json'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'

        // `return void` removes the lint error: `Not all execution paths return a value`.
        return void
    }
    productFlavors {
        dev {
            buildConfigField('String', 'UPDATE_URL',
                    '"https://avnet.ws/.apps/hentoid/debug/update.json"')
            def appId = "me.devsaki.hentoid.dev"
            resValue('string', 'app_id', appId)
            resValue('string', 'app_name', 'Hentoid-dev')
            applicationId appId
        }
        prod {
            buildConfigField('String', 'UPDATE_URL',
                    '"https://raw.githubusercontent.com/AVnetWS/Hentoid/master/app/update.json"')
            def appId = android.defaultConfig.applicationId
            resValue('string', 'app_id', appId)
            resValue('string', 'app_name', 'Hentoid')
            applicationId appId
        }
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix "-DEBUG"
        }
        release {
            debuggable false
            minifyEnabled true

            signingConfig signingConfigs.release

            // Library specific proguard files
            proguardFiles fileTree(dir: '../.proguard', include: ['*.pro']).asList().toArray()

            // Default proguard files & project app specific rules
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project-app.pro'

            // As of Gradle Android plugin 1.1.0, the test APK has a separate config
            testProguardFile 'proguard-project-test.pro'
        }
        qa {
            debuggable true
            minifyEnabled true

            signingConfig signingConfigs.release

            // Library specific proguard files
            proguardFiles fileTree(dir: '../.proguard', include: ['*.pro']).asList().toArray()

            // Default proguard files & project app specific rules
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project-app.pro'

            // As of Gradle Android plugin 1.1.0, the test APK has a separate config
            testProguardFile 'proguard-project-test.pro'

            versionNameSuffix "-QA"
        }
    }
    lintOptions {
        lintConfig file("../lint.xml")
    }
}

def Properties props = new Properties()
def propFile = new File('.signing/signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD')
            && props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    // Compile any included libraries
    compile fileTree(include: ['*jar'], dir: 'libs')
    // Tests
    // junit: github.com/junit-team/junit4/wiki/Use-with-Gradle
    testCompile group: 'junit', name: 'junit', version: '4.12'
    // https://developer.android.com/topic/libraries/testing-support-library/
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // Support libraries
    compile group: 'com.android.support', name: 'support-v13', version: '25.3.1'
    compile group: 'com.android.support', name: 'appcompat-v7', version: '25.3.1'
    compile group: 'com.android.support', name: 'cardview-v7', version: '25.3.1'
    compile group: 'com.android.support', name: 'design', version: '25.3.1'
    // Material Dialogs: github.com/afollestad/material-dialogs
    compile group: 'com.afollestad.material-dialogs', name: 'core', version: '0.9.4.5'
    // Image loader: github.com/bumptech/glide
    //compile group: 'com.github.bumptech.glide', name: 'glide', version: '3.8.0'
    // Integrate with OkHttp: github.com/bumptech/glide/wiki/Integration-Libraries#okhttp-3
    //compile group: 'com.github.bumptech.glide', name: 'okhttp3-integration', version: '1.5.0'
    compile 'com.github.bumptech.glide:glide:4.0.0-RC1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC1'
    compile ("com.github.bumptech.glide:recyclerview-integration:4.0.0-RC1") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    compile "com.github.bumptech.glide:okhttp3-integration:4.0.0-RC1"
    // Intro screens or slides: https://github.com/apl-devs/AppIntro
    compile group: 'com.github.paolorotolo', name: 'AppIntro', version: 'v4.2.0'
    //compile project(':appintro')
    // Google Play Services Analytics: developers.google.com/android/guides/setup
    compile group: 'com.google.android.gms', name: 'play-services-analytics', version: '11.0.4'
    // Java serialization/deserialization (Java Objects into JSON and back): github.com/google/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    // Timber (Extensible Logger): github.com/JakeWharton/timber
    compile group: 'com.jakewharton.timber', name: 'timber', version: '4.5.1'
    // Download app updates (without user prompts): github.com/smanikandan14/ThinDownloadManager
    compile group: 'com.mani', name: 'ThinDownloadManager', version: '1.3.0'
    // Additional Support Libraries
    // HTTP & HTTP/2 client: github.com/square/okhttp
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.8.1'
    // EventBus: github.com/greenrobot/EventBus
    compile group: 'org.greenrobot', name: 'eventbus', version: '3.0.0'
    // JSOUP HTML parser: jsoup.org/download
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.3'
    // RxAndroid (RxJava bindings for Android): github.com/ReactiveX/RxAndroid
    compile group: 'io.reactivex', name: 'rxandroid', version: '1.2.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile group: 'io.reactivex', name: 'rxjava', version: '1.3.0'
    // LeakCanary: https://github.com/square/leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
}

apply plugin: 'com.google.gms.google-services'
